#!/usr/bin/env zsh
emulate -L zsh

# Get preferences from zstyle
local -a handler remote
local editor
zstyle -g handler :zle:evil-registers:handlers:$_evil_register paste
zstyle -g editor :zle:evil-registers:sync editor
zstyle -g remote :zle:evil-registers:sync remote

(( $#remote )) || case $editor in
	(|g)vim*) if [[ $(vim --version) = *+clientserver* ]]; then
		remote=(vim --remote-expr)
		zstyle :zle:evil-registers:sync remote $remote
	fi ;;
	nvim*|neovim*|nvr)
		remote=(nvr --nostart -s --remote-expr)
		zstyle :zle:evil-registers:sync remote $remote
esac

local oldcutbuffer="$CUTBUFFER"

# handle user registers
if (( $#handler )); then
	CUTBUFFER="$($handler)"
	zle .vi-set-buffer ''

elif [[ $_evil_register = ('%'|'#') ]]; then
	# full path of current/alternate buffer
	local reg
	if (( $#remote )); then
		CUTBUFFER="$($remote "expand('$_evil_register:p')")"
	else # TODO: more editors
		CUTBUFFER="$PWD"
	fi
	zle .vi-set-buffer ''

elif [[ $_evil_register = '/' ]]; then
	CUTBUFFER="$LASTSEARCH"

elif [[ $_evil_register = '.' && -v __last_insert ]]; then
	CUTBUFFER="$__last_insert"

elif [[ $_evil_register = [a-zA-Z] ]]; then
	local reg
	if (( ${#remote} )); then
		reg="$($remote "getreg(\"$_evil_register\")")"
	else
		# TODO: more editors
	fi
	(( $#reg )) && registers[$_evil_register]="$reg"
fi
zle ".$WIDGET"
CUTBUFFER="$oldcutbuffer"
unset _evil_register
