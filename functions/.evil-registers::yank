#!/usr/bin/env zsh
emulate -L zsh
setopt extendedglob
zmodload zsh/sched

# Get preferences from zstyle
local -a handler ahandler

# On first run, setup editor
if (($+functions[.evil-registers::setup-editor])); then
	.evil-registers::setup-editor
	unfunction .evil-registers::setup-editor
fi

zstyle -a :zle:evil-registers:${(q)_evil_register} yank   handler
zstyle -a :zle:evil-registers:${(q)_evil_register} yanka ahandler

# Let direct handlers override register handlers
if (( $#handler )); then
	zle .vi-set-buffer x
	local x
	x=$registers[x]
	zle .$WIDGET
	$handler <<< $registers[x]
	registers[x]=$x
elif (($#ahandler)); then

	# append or set remote
	local old
	if [[ $_evil_register = [A-Z] ]]; then
		zle .vi-set-buffer "${_evil_register:l}"
		old=$registers[${_evil_register:l}]
		zle .$WIDGET
	else
		zle .$WIDGET
	fi
	# Use sched to execute without job control messages
	# sched splits the parameters
	sched +0 $ahandler "$_evil_register" "${(q)registers[${_evil_register:l}]}"
	registers[${_evil_register:l}]="$old${registers[${_evil_register:l}]}"
else
	zle .$WIDGET
fi

unset _evil_register
