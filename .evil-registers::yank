#!/usr/bin/env zsh
emulate -L zsh
setopt extendedglob
zmodload zsh/sched

# Let handlers override normal [[:alnum:]_] registers
if (( ${+ZSH_EVIL_COPY_HANDLERS[$_evil_register]} )); then
	zle .vi-set-buffer x
	local x
	x="${registers[x]}"
	zle "$1"
	eval ${ZSH_EVIL_COPY_HANDLERS[$_evil_register]} <<< "${registers[x]}"
	registers[x]="$x"
	unset _evil_register
	return
fi

case $_evil_register in

''|[0-9_]) zle "$1" ;;
[a-zA-Z]) # sync
	if ! typeset +f .evil-registers::remote-set >/dev/null; then
		case $ZSH_EVIL_SYNC_EDITOR in
		nvim*|neovim*|nvr)
			(( $+commands[nvr] )) &&
				.evil-registers::remote-set(){
					local MATCH MBEGIN MEND
					timeout -k 15 10 nvr -s --nostart --remote-expr \
					"setreg('$1', \"${2//(#m)[\\\"]/\\$MATCH}\")" >/dev/null
				}
			;;
		(|g)vim*)
			# parse version to detect server support
			[[ $(vim --version) = *+clientserver* ]] &&
				.evil-registers::remote-set(){
					local MATCH
					local -i MBEGIN MEND
					timeout -k 15 10 vim --servername "${VIMSERVER:="$ZSH_EVIL_SYNC_EDITOR"}" --remote-expr \
					"setreg('$1', \"${2//(#m)[\\\"]/\\$MATCH}\")" >/dev/null
				}
			;;
		*)
			# no match found, do default zle and exit
			zle "$1"
			unset _evil_register
			return
			;;
		esac

	fi
	# append or set remote
	local old
	if [[ $_evil_register = [A-Z] ]]; then
		zle .vi-set-buffer "${_evil_register:l}"
		old=$registers[${_evil_register:l}]
		zle "$1"
	else zle "$1"; fi
	# Use sched to execute without job control messages
	# sched splits the parameters
	sched +0 .evil-registers::remote-set "$_evil_register" "${(q)registers[${_evil_register:l}]}"
	registers[${_evil_register:l}]="$old${registers[${_evil_register:l}]}"
;;
*) zle "$1" ;;

esac

unset _evil_register
